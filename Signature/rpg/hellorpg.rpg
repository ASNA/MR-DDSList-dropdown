        Ctl-Opt Option(*srcstmt) Dftactgrp(*No) ActGrp('rpmobile');

        Dcl-F HelloDspf                  WORKSTN Infds(infds)
                                            Handler('MOBILERPG')
                                            SFile(SBFDD:SbfRRN);

        /COPY RPMRDATA/QRPGLESRC,KEYMAPF

        Dcl-S SbfRRN   Zoned(4:0);

        Dcl-DS States LikeDS(StateInfo) Dim(100);

        Dcl-DS StateInfo Qualified;
            State  Char(48);
            Abbrev Char(2);
        End-DS;

        Dcl-DS Action Qualified;
            Exit           Char(1) Inz(F03);
            Cancel         Char(1) Inz(F02);
            Back           Char(1) Inz(F02);
            ItemTapped     Char(1) Inz(F01);
            ChevronTapped  Char(1) Inz(F04);
            More           Char(1) Inz(F05);
            Backward       Char(1) Inz(F06);
            Find           Char(1) Inz(F07);
            OK             Char(1) Inz(F08);
            Display        Char(1) Inz(F09);
            Remove         Char(1) Inz(F10);
        End-DS;                        

        LoadStatesList();
        // Set DDCurrVal to start the list at a specific
        // list item.
        //DDCurrVal = 'IL';
        ExFmt HOMEMENU;

        Dow ActionRequest <> Action.Exit;
            Select;
                When CurrentFormat = 'HOMEMENU';
                    Select; 
                        When ActionRequest = Action.OK;
                            // Selected value of states dropdown
                            // is in DDCurrVal.
                            // Assign it to a visible field.
                            State = DDCurrVal;
                            LoadStatesList();
                            ExFmt HOMEMENU;
                        Other;
                            LoadStatesList();
                            ExFmt HOMEMENU;                    
                    EndSL;                            
            EndSl;
        EndDo;         

        *INLR = *ON;
        Return;

        Dcl-Proc LoadStatesList;
            Dcl-Pi *N;
            End-Pi;            

            Dcl-S RowsReturned Int(10);
            Dcl-S Index Int(10);

            //RowsReturned = GetStates();
            RowsReturned = GetStatesSimple();

            *IN99 = *On;
            Write SBFDDCTL;
            SbfRRN = 0;

            For Index=1 To RowsReturned;
                DDText = States(Index).State;
                DDValue = States(Index).Abbrev;
                DDSel = '0';
                SbfRRN = SbfRRN + 1;
                Write SbfDD;
            EndFor;

            *In99 = *Off;
            Write SBFDDCTL;
        End-Proc;

        Dcl-Proc GetStates;
            Dcl-Pi *N Int(10);
            End-Pi;            

            Dcl-S RowsReturned Int(10);

            Dcl-Pr GetStatesList ExtPgm('STLST');
                *N    LikeDS(StateInfo) Dim(100);
                *N    int(10);
            End-Pr;               

            CallP GetStatesList(States : RowsReturned);
            Return RowsReturned;
        End-Proc;

        Dcl-Proc GetStatesSimple;
            Dcl-Pi *N Int(10);
            End-Pi;            

            Dcl-S Index Int(10);
            
            Index = 1;
            States(Index).State = 'Alabama';
            States(Index).Abbrev = 'AL';    

            Index = 2;
            States(Index).State = 'Colorado';
            States(Index).Abbrev = 'CO';    

            Index = 3;
            States(Index).State = 'Illinois';
            States(Index).Abbrev = 'IL';    

            Index = 4;
            States(Index).State = 'Georgia';
            States(Index).Abbrev = 'NV';    

            Index = 5;
            States(Index).State = 'Nevada';
            States(Index).Abbrev = 'NV';    

            Return 5;
        End-Proc;         
